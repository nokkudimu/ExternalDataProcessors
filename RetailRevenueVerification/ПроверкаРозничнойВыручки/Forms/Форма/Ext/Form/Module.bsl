
&НаСервере
Процедура ОбновитьСписокНаСервере() 
	Объект.РозничнаяВыручка.Очистить();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(Период.ДатаНачала));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Период.ДатаОкончания));
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("КассаККМ", КассаККМ);
	Текст = "ВЫБРАТЬ
	|	        	ОтчетОРозничныхПродажах.Ссылка КАК Ссылка,
	|	        	ОтчетОРозничныхПродажах.Организация КАК Организация,
	|	        	ОтчетОРозничныхПродажах.Дата КАК Дата,
	|	        	ОтчетОРозничныхПродажах.КассаККМ КАК КассаККМ,
	|	        	ОтчетОРозничныхПродажах.СуммаДокумента КАК СуммаДокумента,
	|	        	ЕСТЬNULL(ОтчетОРозничныхПродажахОплатаПлатежнымиКартами.Сумма, 0) КАК СуммаБезнал,
	|	        	ОтчетОРозничныхПродажах.СуммаДокумента - ЕСТЬNULL(ОтчетОРозничныхПродажахОплатаПлатежнымиКартами.Сумма, 0) КАК СуммаНал
	|	        ПОМЕСТИТЬ ВТ_Основн
	|	        ИЗ
	|	        	Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
	|	        		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.ОплатаПлатежнымиКартами КАК ОтчетОРозничныхПродажахОплатаПлатежнымиКартами
	|	        		ПО ОтчетОРозничныхПродажах.Ссылка = ОтчетОРозничныхПродажахОплатаПлатежнымиКартами.Ссылка
	|	        ГДЕ
	|	        	ОтчетОРозничныхПродажах.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	        	И ОтчетОРозничныхПродажах.Проведен
	|	        	И ОтчетОРозничныхПродажах.Склад = &Склад
	|	        	И ОтчетОРозничныхПродажах.Организация = &Организация
	|	        	И ОтчетОРозничныхПродажах.КассаККМ = &КассаККМ
	|	        ;
	|	        
	|	        ////////////////////////////////////////////////////////////////////////////////
	|	        ВЫБРАТЬ
	|	        	ВТ_Основн.Ссылка КАК Ссылка,
	|	        	ВТ_Основн.Организация КАК Организация,
	|	        	ВТ_Основн.Дата КАК Дата,
	|	        	ВТ_Основн.КассаККМ КАК КассаККМ,
	|	        	ВТ_Основн.СуммаДокумента КАК СуммаДокумента,
	|	        	ВТ_Основн.СуммаБезнал КАК СуммаБезнал,
	|	        	ВТ_Основн.СуммаНал КАК СуммаНал,
	|	        	СтатусыПроверкиДокументов.СтатусПроверки КАК СтатусПроверки
	|	        ИЗ
	|	        	ВТ_Основн КАК ВТ_Основн
	|	        		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПроверкиДокументов КАК СтатусыПроверкиДокументов
	|	        		ПО ВТ_Основн.Ссылка = СтатусыПроверкиДокументов.Документ.Ссылка
	|";
	Если НЕ ЗначениеЗаполнено(КассаККМ) Тогда
		Текст = СтрЗаменить(Текст, "И ОтчетОРозничныхПродажах.КассаККМ = &КассаККМ", "");		
	КонецЕсли;
	Запрос.Текст = Текст;
	Выборка = Запрос.Выполнить().Выбрать();
	
	СуммаНал = 0;
	СуммаБН = 0;
	СуммаОбщая = 0;
	
	Пока Выборка.Следующий() Цикл
		
		СтрТЧ = Объект.РозничнаяВыручка.Добавить();
		СтрТЧ.Дата = Выборка.Дата;
		СтрТЧ.КассаККМ = Выборка.КассаККМ;
		СтрТЧ.ОтчетОРозничныхПродажах = Выборка.Ссылка;
		СтрТЧ.ВыручкаНаличная = Выборка.СуммаНал;
		СтрТЧ.ВыручкаБезналичная = Выборка.СуммаБезнал;
		СтрТЧ.СтатусПроверки = Выборка.СтатусПроверки;
		СтрТЧ.ВыручкаОбщая = Выборка.СуммаДокумента; 
		СтрТЧ.ФактическаяСуммаНал = Выборка.СуммаНал;
		СтрТЧ.ФактическаяСуммаБН = Выборка.СуммаБезнал;
		СтрТЧ.СходитсяНал = Истина;
		СтрТЧ.СходитсяБН = Истина;
		СтрТЧ.ПроверитьДокумент = Истина;
		
		СуммаНал = СуммаНал + СтрТЧ.ФактическаяСуммаНал;
		СуммаБН = СуммаБН + СтрТЧ.ФактическаяСуммаБН;
		СуммаОбщая = СуммаОбщая + СтрТЧ.ФактическаяСуммаНал + СтрТЧ.ФактическаяСуммаБН;
		
	КонецЦикла;
	
	ДокСуммаНал = СуммаНал;
	ДокСуммаБН = СуммаБН;
	ДокСуммаОбщая = СуммаОбщая;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)
	Результат = ПроверитьОбязательныеРеквизитыФормы();
	НезаполненныеРеквизиты = "";
	
	Для каждого Ошибка Из Результат Цикл
		Если Ошибка.Значение Тогда
			НезаполненныеРеквизиты = НезаполненныеРеквизиты + " " + Ошибка.Ключ;			
		КонецЕсли;		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(НезаполненныеРеквизиты) Тогда
		НезаполненныеРеквизиты = СокрЛП(НезаполненныеРеквизиты);
		Сообщить("Заполните следующие реквизиты: " + НезаполненныеРеквизиты);
		Возврат;
	КонецЕсли;
	
	ОбновитьСписокНаСервере();
КонецПроцедуры

&НаКлиенте
Функция ПроверитьОбязательныеРеквизитыФормы()
	
	Ошибки = Новый Структура;
	Ошибки.Вставить("Период", ?(НЕ ЗначениеЗаполнено(Период), Истина, Ложь));
	Ошибки.Вставить("Организация", ?(НЕ ЗначениеЗаполнено(Организация), Истина, Ложь));
	Ошибки.Вставить("Склад", ?(НЕ ЗначениеЗаполнено(Склад), Истина, Ложь)); 
	
	Возврат Ошибки;
	
КонецФункции // ()


&НаКлиенте
Процедура РозничнаяВыручкаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) 
	Если Поле.Имя = "РозничнаяВыручкаОтчетОРозничныхПродажах" Тогда
		
		СтандартнаяОбработка = Ложь;
		Парам = Новый Структура;
		Парам.Вставить("Ключ", Элемент.ТекущиеДанные.ОтчетОРозничныхПродажах);
		ОткрытьФорму("Документ.ОтчетОРозничныхПродажах.Форма.ФормаДокумента", Парам);		
		
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура РозничнаяВыручкаФактическаяСуммаНалПриИзменении(Элемент)
	ПроверитьСуммыВыручек(Элемент);
	ПересчитатьСуммы();
КонецПроцедуры

&НаКлиенте
Процедура РозничнаяВыручкаФактическаяСуммаБНПриИзменении(Элемент)
	ПроверитьСуммыВыручек(Элемент);
	ПересчитатьСуммы();
КонецПроцедуры  

&НаКлиенте
Процедура ПроверитьСуммыВыручек(ТекЭлемент)
	
	//ТекЭлемент.Родитель.ТекущиеДанные.ВыручкаБезналичная - пример элемента формы с необходимым значением 
	ФактНал = ТекЭлемент.Родитель.ТекущиеДанные.ФактическаяСуммаНал;
	ФактБН = ТекЭлемент.Родитель.ТекущиеДанные.ФактическаяСуммаБН;
	ДокНал = ТекЭлемент.Родитель.ТекущиеДанные.ВыручкаНаличная;
	ДокБН = ТекЭлемент.Родитель.ТекущиеДанные.ВыручкаБезналичная;
	
	ТекЭлемент.Родитель.ТекущиеДанные.СходитсяНал = ?(ФактНал = ДокНал, Истина, Ложь);
	ТекЭлемент.Родитель.ТекущиеДанные.СходитсяБН = ?(ФактБН = ДокБН, Истина, Ложь);
	
	Если ТекЭлемент.Родитель.ТекущиеДанные.СходитсяНал И ТекЭлемент.Родитель.ТекущиеДанные.СходитсяБН Тогда	
		
		ТекЭлемент.Родитель.ТекущиеДанные.ПроверитьДокумент = Истина;
		
	Иначе
		
		ТекЭлемент.Родитель.ТекущиеДанные.ПроверитьДокумент = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСуммы()
	
	СуммаНал = 0;
	СуммаБН = 0;
	СуммаОбщая = 0;
	
	Для каждого стрТЧ Из Объект.РозничнаяВыручка Цикл
		СуммаНал = СуммаНал + СтрТЧ.ФактическаяСуммаНал;
		СуммаБН = СуммаБН + СтрТЧ.ФактическаяСуммаБН;
		СуммаОбщая = СуммаОбщая + СтрТЧ.ФактическаяСуммаНал + СтрТЧ.ФактическаяСуммаБН;		
	КонецЦикла;
	
	ДокСуммаНал = СуммаНал;
	ДокСуммаБН = СуммаБН;
	ДокСуммаОбщая = СуммаОбщая;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОбновитьСписокКассККМ();
КонецПроцедуры 

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	ОбновитьСписокКассККМ();
КонецПроцедуры

// обновление списка выбора касс ККМ
&НаСервере
Процедура ОбновитьСписокКассККМ()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Склад", Склад);
	Текст = "ВЫБРАТЬ
	|	КассыККМ.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КассыККМ КАК КассыККМ
	|ГДЕ
	|	НЕ КассыККМ.ПометкаУдаления
	|	И КассыККМ.Владелец = &Организация
	|	И КассыККМ.Склад = &Склад";
	
	Если НЕ ЗначениеЗаполнено(Организация) И НЕ ЗначениеЗаполнено(Склад) Тогда // 0 0
		
		Запрос.Текст = СтрЗаменить(Текст, "И КассыККМ.Владелец = &Организация
		|	И КассыККМ.Склад = &Склад", "");
		
	ИначеЕсли НЕ ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Склад) Тогда // 0 1	
		
		Запрос.Текст = СтрЗаменить(Текст, "И КассыККМ.Владелец = &Организация", "");
		
	ИначеЕсли ЗначениеЗаполнено(Организация) И НЕ ЗначениеЗаполнено(Склад) Тогда // 1 0	
		
		Запрос.Текст = СтрЗаменить(Текст, "И КассыККМ.Склад = &Склад", "");
		
	Иначе // 1 1 - default	
		
		Запрос.Текст = Текст;
		
	КонецЕсли;
	
	Массив_ = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Массив_.Добавить(Выборка.Ссылка);	
	КонецЦикла;
	
	Элементы.КассаККМ.РежимВыбораИзСписка = Истина;
	Элементы.КассаККМ.СписокВыбора.ЗагрузитьЗначения(Массив_);
	
КонецПроцедуры

&НаКлиенте
Процедура РозничнаяВыручкаПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СформироватьСообщение(Команда)
	ТелоСообщения = "";
	Для каждого стрТЧ Из Объект.РозничнаяВыручка Цикл
		СообщениеСтр = "";
		Если НЕ стрТЧ.ПроверитьДокумент Тогда
			СообщениеСтр = "В документе " + стрТЧ.ОтчетОРозничныхПродажах + " не соответствуют суммы: ";
			Если стрТЧ.ВыручкаНаличная <> стрТЧ.ФактическаяСуммаНал Тогда
				
				Разница = стрТЧ.ФактическаяСуммаНал - стрТЧ.ВыручкаНаличная;
				Усл = ?(Разница > 0, " меньше ", " больше ");
				МодульЧисла = ?(Разница > 0, Разница, -Разница);
				СообщениеСтр = СообщениеСтр + "наличных" + Усл + "на " + МодульЧисла + "; "; 	
				
			КонецЕсли;
			Если стрТЧ.ВыручкаБезналичная <> стрТЧ.ФактическаяСуммаБН Тогда
				
				Разница = стрТЧ.ФактическаяСуммаБН - стрТЧ.ВыручкаБезналичная;
				Усл = ?(Разница > 0, " меньше ", " больше ");
				МодульЧисла = ?(Разница > 0, Разница, -Разница);
				СообщениеСтр = СообщениеСтр + "безналичных" + Усл + "на " + МодульЧисла + "; "; 	
				
			КонецЕсли;
			СообщениеСтр = СообщениеСтр + "Должно быть: нал - " + стрТЧ.ФактическаяСуммаНал + ", б/н - " + стрТЧ.ФактическаяСуммаБН + ";";
		КонецЕсли;
		Если ЗначениеЗаполнено(СообщениеСтр) Тогда
			ТелоСообщения = ТелоСообщения + СообщениеСтр + Символы.ПС;	
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеЗаполнено(ТелоСообщения) Тогда
		СообщениеОРасхождениях_ = "По магазину " + Склад + " (КассаККМ: $$$)" + " за период " 
		+ Строка(Период) + " найдены расхождения в следующих документах:" + Символы.ПС + Символы.ПС
		+ ТелоСообщения;
		Если ЗначениеЗаполнено(КассаККМ) Тогда
			
			СообщениеОРасхождениях_ = СтрЗаменить(СообщениеОРасхождениях_, "$$$", Строка(КассаККМ));		
			
		Иначе
			
			СообщениеОРасхождениях_ = СтрЗаменить(СообщениеОРасхождениях_, " (КассаККМ: $$$)", "");
			
		КонецЕсли;
		СообщениеоРасхождениях = СообщениеОРасхождениях_;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПроверитьДокументыНаСервере()
	Для каждого стрТЧ Из Объект.РозничнаяВыручка Цикл
		
		Если стрТЧ.ПроверитьДокумент Тогда
			
			// изменяем докОб
			докОб = стрТЧ.ОтчетОРозничныхПродажах.ПолучитьОбъект();
			докОб.Подразделение = докОб.КассаККМ.Подразделение;
			докОб.Записать(РежимЗаписиДокумента.Проведение);
			
			// проверка
			НаборЗаписей = РегистрыСведений.СтатусыПроверкиДокументов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Документ.Установить(стрТЧ.ОтчетОРозничныхПродажах);
			НоваяЗапись = НаборЗаписей.Добавить();      
			НоваяЗапись.Документ = стрТЧ.ОтчетОРозничныхПродажах;    			
			
			НоваяЗапись.СтатусПроверки = Перечисления.СтатусыПроверкиФинансовыхДокументов.Проверен;
			Пользователь = Справочники.Пользователи.НайтиПоНаименованию(ПользователиИнформационнойБазы.ТекущийПользователь().Имя);
			НоваяЗапись.Проверил = Пользователь;
			НоваяЗапись.ДатаПроверки = ТекущаяДатаСеанса();
			НоваяЗапись.Изменил = Пользователь;    
			НоваяЗапись.ДатаИзменения = ТекущаяДатаСеанса(); 
			НоваяЗапись.Период = ТекущаяДатаСеанса();
			НаборЗаписей.Записать(Истина);	
			// регл. учёт
			СтруктураРеквизиты = Новый Структура("Ссылка, Дата, Организация", стрТЧ.ОтчетОРозничныхПродажах, стрТЧ.Дата, Организация); 
			РеглУчетПроведениеСервер.ОтразитьДокумент(СтруктураРеквизиты);
			
		КонецЕсли;	
		
	КонецЦикла; 
	КолвоСтрокТЧ = Объект.РозничнаяВыручка.Количество() - 1;
	ЗаполненныйНалТЗ = Объект.РозничнаяВыручка.Выгрузить(, "ФактическаяСуммаНал");   
	ЗаполненныйБНТЗ = Объект.РозничнаяВыручка.Выгрузить(, "ФактическаяСуммаБН"); 
	ЗаполненныйНалМассив = Новый Массив;
	ЗаполненныйБНМассив = Новый Массив;
	Для Индекс = 0 По КолвоСтрокТЧ Цикл
		
		ЗаполненныйНалМассив.Добавить(ЗаполненныйНалТЗ[Индекс].ФактическаяСуммаНал);
		ЗаполненныйБНМассив.Добавить(ЗаполненныйБНТЗ[Индекс].ФактическаяСуммаБН);
		
	КонецЦикла;
	ОбновитьСписокНаСервере();
	ОбновленнаяТЗ = Объект.РозничнаяВыручка.Выгрузить();  
	ОбновленнаяТЗ.ЗагрузитьКолонку(ЗаполненныйНалМассив, "ФактическаяСуммаНал");
	ОбновленнаяТЗ.ЗагрузитьКолонку(ЗаполненныйБНМассив, "ФактическаяСуммаБН");
	Объект.РозничнаяВыручка.Загрузить(ОбновленнаяТЗ);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьДокументы(Команда)
	ПроверитьДокументыНаСервере();
КонецПроцедуры





