
&НаСервере
Процедура ПолучитьСписокНаСервере()
	Объект.ДокументыБезСФ.Очистить();
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНачала", Период.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", Период.ДатаОкончания); 
	Запрос.УстановитьПараметр("Поставщик", Поставщик);
	Запрос.УстановитьПараметр("Организация", Организация);
	Текст = "ВЫБРАТЬ
	        |	ПриобретениеТоваровУслугТовары.Ссылка.Проведен КАК Проведен,
	        |	ПриобретениеТоваровУслугТовары.Ссылка КАК Ссылка,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.Номер КАК Номер,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.Дата КАК Дата,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.Партнер КАК Партнер,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.СуммаДокумента КАК СуммаДокумента,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.Договор КАК Договор,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.Организация КАК Организация,
	        |	СУММА(ПриобретениеТоваровУслугТовары.СуммаНДС) КАК СуммаНДС,
	        |	ВЫРАЗИТЬ(ПриобретениеТоваровУслугТовары.Ссылка.ПХ_СчетФактураНомер КАК СТРОКА(1000)) КАК ПХ_СчетФактураНомер,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.ПХ_СчетФактураДата КАК ПХ_СчетФактураДата
	        |ПОМЕСТИТЬ ВТ_ПриобретенияВозвраты
	        |ИЗ
	        |	Документ.ПриобретениеТоваровУслуг.Товары КАК ПриобретениеТоваровУслугТовары
	        |ГДЕ
	        |	ПриобретениеТоваровУслугТовары.Ссылка.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	        |	И ПриобретениеТоваровУслугТовары.Ссылка.Проведен
			|	И &Alter
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ПриобретениеТоваровУслугТовары.Ссылка.Проведен,
	        |	ПриобретениеТоваровУслугТовары.Ссылка,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.Номер,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.Дата,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.Партнер,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.СуммаДокумента,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.НомерВходящегоДокумента,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.ДатаВходящегоДокумента,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.Договор,
	        |	ПриобретениеТоваровУслугТовары.Ссылка.Организация,
	        |	ВЫРАЗИТЬ(ПриобретениеТоваровУслугТовары.Ссылка.ПХ_СчетФактураНомер КАК СТРОКА(1000)),
	        |	ПриобретениеТоваровУслугТовары.Ссылка.ПХ_СчетФактураДата
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ПриобретенияВозвраты.Ссылка.Проведен КАК Проведен,
	        |	ВТ_ПриобретенияВозвраты.Ссылка КАК Ссылка,
	        |	ВТ_ПриобретенияВозвраты.Номер КАК Номер,
	        |	ВТ_ПриобретенияВозвраты.Дата КАК Дата,
	        |	ВТ_ПриобретенияВозвраты.Партнер КАК Партнер,
	        |	ВТ_ПриобретенияВозвраты.СуммаДокумента КАК СуммаДокумента,
	        |	ВТ_ПриобретенияВозвраты.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	        |	ВТ_ПриобретенияВозвраты.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	        |	ВТ_ПриобретенияВозвраты.Договор КАК Договор,
	        |	ВТ_ПриобретенияВозвраты.Организация КАК Организация,
	        |	ВТ_ПриобретенияВозвраты.СуммаНДС КАК СуммаНДС,
	        |	ВТ_ПриобретенияВозвраты.ПХ_СчетФактураНомер КАК ПХ_СчетФактураНомер,
	        |	ВТ_ПриобретенияВозвраты.ПХ_СчетФактураДата КАК ПХ_СчетФактураДата
	        |ИЗ
	        |	ВТ_ПриобретенияВозвраты КАК ВТ_ПриобретенияВозвраты
	        |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	        |		ПО ВТ_ПриобретенияВозвраты.Ссылка = СчетФактураПолученныйДокументыОснования.ДокументОснование.Ссылка
	        |ГДЕ
	        |	(НЕ СчетФактураПолученныйДокументыОснования.Ссылка.Проведен
	        |			ИЛИ СчетФактураПолученныйДокументыОснования.Ссылка ЕСТЬ NULL)
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТ_ПриобретенияВозвраты.Ссылка.Проведен,
	        |	ВТ_ПриобретенияВозвраты.Ссылка,
	        |	ВТ_ПриобретенияВозвраты.Номер,
	        |	ВТ_ПриобретенияВозвраты.Дата,
	        |	ВТ_ПриобретенияВозвраты.Партнер,
	        |	ВТ_ПриобретенияВозвраты.СуммаДокумента,
	        |	ВТ_ПриобретенияВозвраты.НомерВходящегоДокумента,
	        |	ВТ_ПриобретенияВозвраты.ДатаВходящегоДокумента,
	        |	ВТ_ПриобретенияВозвраты.Договор,
	        |	ВТ_ПриобретенияВозвраты.Организация,
	        |	ВТ_ПриобретенияВозвраты.СуммаНДС,
	        |	ВТ_ПриобретенияВозвраты.ПХ_СчетФактураНомер,
	        |	ВТ_ПриобретенияВозвраты.ПХ_СчетФактураДата
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	Дата";

	
	//Все
	Если ЗначениеЗаполнено(Поставщик) И ЗначениеЗаполнено(Организация) Тогда
		
		ТекстЗапроса = СтрЗаменить(Текст, "И &Alter", "И ПриобретениеТоваровУслугТовары.Ссылка.Контрагент = &Поставщик
	|	И ПриобретениеТоваровУслугТовары.Ссылка.Организация = &Организация");
		
		//+Поставщик -Организация
	ИначеЕсли ЗначениеЗаполнено(Поставщик) И НЕ ЗначениеЗаполнено(Организация) Тогда
		
		ТекстЗапроса = СтрЗаменить(Текст, "И &Alter",
		"И ПриобретениеТоваровУслугТовары.Ссылка.Контрагент = &Поставщик");
		
		//-Поставщик +Организация	
	ИначеЕсли НЕ ЗначениеЗаполнено(Поставщик) И ЗначениеЗаполнено(Организация) Тогда  
		
		ТекстЗапроса = СтрЗаменить(Текст, "И &Alter",
		"И ПриобретениеТоваровУслугТовары.Ссылка.Организация = &Организация");
		
		//Ничего	
	Иначе
		
		ТекстЗапроса = СтрЗаменить(Текст, "И &Alter",
		"");
		
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		// добавляем строки тч
		СтрТЧ = Объект.ДокументыБезСФ.Добавить();
		СтрТЧ.Документ = Выборка.Ссылка;
		СтрТЧ.Номер = Выборка.Номер;
		СтрТЧ.Дата = Выборка.Дата;
		СтрТЧ.Поставщик = Выборка.Партнер;
		СтрТЧ.Организация = Выборка.Организация;
		СтрТЧ.Договор = Выборка.Договор;
		СтрТЧ.СуммаНДС = Выборка.СуммаНДС;
		СтрТЧ.СуммаДокумента = Выборка.СуммаДокумента;
		// берутся из новых полей в документе
		СтрТЧ.НомерСФ = Выборка.ПХ_СчетФактураНомер;
		СтрТЧ.ДатаСФ = Выборка.ПХ_СчетФактураДата;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСписок(Команда)
	// проверка реквизитов
	Если НЕ ПроверитьЗаполненностьФормы() Тогда
		Возврат;	
	КонецЕсли;
	ПолучитьСписокНаСервере();
КонецПроцедуры

&НаКлиенте
Функция ПроверитьЗаполненностьФормы()

	Если НЕ ЗначениеЗаполнено(Период) Тогда
	
		Сообщить("Поле 'Период' не заполнено!");
		Возврат Ложь;
	
	КонецЕсли;	
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ДокументыБезСФВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ДокументыБезСФДокумент" Тогда
	
		СтандартнаяОбработка = Ложь;
		Парам = Новый Структура;
		Парам.Вставить("Ключ", Элемент.ТекущиеДанные.Документ);
		ОткрытьФорму("Документ.ПриобретениеТоваровУслуг.Форма.ФормаДокумента", Парам);		
	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СоздатьСчетФактурыНаСервере()
	Для каждого элем Из Объект.ДокументыБезСФ Цикл
		Если ЗначениеЗаполнено(элем.НомерСФ) И ЗначениеЗаполнено(элем.ДатаСФ) Тогда
		// создаем док. "СчетФактураПолученный"
		НоваяСФ = Документы.СчетФактураПолученный.СоздатьДокумент();
		ЗаполнитьРеквизитыДокументаСФ(НоваяСФ, элем.Документ, элем.НомерСФ, элем.ДатаСФ);
		ЗаполнитьРеквизитыТЧДокументаСФ(НоваяСФ, элем.Документ, элем.СуммаНДС);
		Попытка
		
			НоваяСФ.Записать(РежимЗаписиДокумента.Проведение);
			// записываем результат в ТЧ
			ДобавитьЗаписьВРезультат(элем.Документ, НоваяСФ.Ссылка);
		
		Исключение
			Сообщить("Не удалось создать счет-фактуру по: " + элем.Документ);
		КонецПопытки;
		
		КонецЕсли;
	КонецЦикла;
	// обновляем ТЧ
	ПолучитьСписокНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСчетФактуры(Команда)
	СоздатьСчетФактурыНаСервере();
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьРеквизитыДокументаСФ(СФ, ТекОбъект, НомерСФ, ДатаСФ)
	//ПараметрыРегистрации = Документы.ПриобретениеТоваровУслуг.ПараметрыРегистрацииСчетовФактурПолученных(ТекущийОбъект);
	СФ.Номер = НомерСФ;
	СФ.Дата = ДатаСФ;
	СФ.Организация = ТекОбъект.Организация;
	СФ.Исправление = Ложь;
	СФ.НомерИсправления = "";
	СФ.ДатаИсправления = Дата("00010101");
	СФ.Корректировочный = Ложь;
	СФ.СчетФактураОснование = Документы.СчетФактураПолученный.ПустаяСсылка();
	СФ.ПолученВЭлектронномВиде = Ложь;
	СФ.Валюта = Справочники.Валюты.НайтиПоНаименованию("RUB");
	СФ.КодВидаОперации = "01";
	СФ.Контрагент = ТекОбъект.Контрагент;
	СФ.ДатаСоставления = НачалоДня(ТекОбъект.Дата); 
	СФ.ОтнестиКПредыдущемуОтчетномуКварталу = Ложь;
	СФ.ДатаЗаписиКнигиПокупок = НачалоДня(ТекОбъект.Дата);
	СФ.Подразделение = ТекОбъект.Подразделение;
	СФ.Ответственный = Пользователи.ТекущийПользователь();
	СФ.СоставленКомиссионеромОтИмениПродавца = Ложь;
	СФ.СводныйКомиссионный = Ложь;
	СФ.КодВидаОперацииНаУменьшение = "18";
	СФ.СводныйКорректировочный = Ложь;
	СФ.ИННКонтрагента = ТекОбъект.Контрагент.ИНН;
	СФ.КППКонтрагента = ТекОбъект.Контрагент.КПП;
	СФ.НаправлениеДеятельности = Справочники.НаправленияДеятельности.ПустаяСсылка();
	СФ.Партнер = текОбъект.Партнер;
	СФ.Договор = ТекОбъект.Договор;
	СФ.Склад = ТекОбъект.Склад;
	СФ.РучнаяКорректировкаЖурналаСФ = Ложь;
	СФ.РучнаяКорректировкаСуммДокумента = Ложь;
	СФ.ДатаЗаписиКнигиПродаж = НачалоДня(ТекОбъект.Дата);
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьРеквизитыТЧДокументаСФ(СФ, ТекОбъект, СуммаНДС)
	СтрТЧ = СФ.ДокументыОснования.Добавить();
	СтрТЧ.ДокументОснование = ТекОбъект.Ссылка;
	СтрТЧ.ХозяйственнаяОперация = ТекОбъект.ХозяйственнаяОперация;
	СтрТЧ.ИсходныйДокумент = Документы.СчетФактураПолученный.ПустаяСсылка();
	СтрТЧ.СтавкаНДС = ТекОбъект.Договор.СтавкаНДС;
	СтрТЧ.Сумма = ТекОбъект.СуммаДокумента; 
	СтрТЧ.СуммаНДС = СуммаНДС;
	СтрТЧ.СуммаУвеличение = 0;
	СтрТЧ.СуммаУменьшение = 0;
	СтрТЧ.СуммаНДСУвеличение = 0;
	СтрТЧ.СуммаНДСУменьшение = 0;
	СтрТЧ.СуммаКомиссия = 0;
	СтрТЧ.СуммаНДСКомиссия = 0;
	СтрТЧ.СуммаУвеличениеКомиссия = 0;
	СтрТЧ.СуммаУменьшениеКомиссия = 0;
	СтрТЧ.СуммаНДСУвеличениеКомиссия = 0;
	СтрТЧ.СуммаНДСУменьшениеКомиссия = 0;
	СтрТЧ.ВидЦенности = Перечисления.ВидыЦенностей.ПустаяСсылка();
	СтрТЧ.ПринятоКВычетуНДСДоВводаОстатков = 0; 
	СтрТЧ.ИдентификаторСтроки = "";  
	СтрТЧ.СтавкаНДСДоВводаОстатков = Справочники.СтавкиНДС.ПустаяСсылка();
КонецПроцедуры

&НаСервере
Процедура ДобавитьЗаписьВРезультат(Приобретение, СчетФактура)

	СтрТЧ = Объект.СозданныеСФ.Добавить();
	СтрТЧ.ДокументПриобретения = Приобретение;
	СтрТЧ.СчетФактура = СчетФактура;

КонецПроцедуры

